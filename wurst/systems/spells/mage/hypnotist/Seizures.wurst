package Seizures
import RegisterEvents
import ClosureTimers
import Orders
import DummyCaster
import ChannelAbilityPreset
import Lodash
import Assets
import ToolTipsUtils

@configurable constant int ABILITY_ID = 'A05T'
@configurable constant int DUMMY_ABILITY_ID = 'A05U'

let MANACOST = 20
let COOLDOWN = 30.

let ANIMATION_NAME = "channel,spell"
let TOOLTIP_NORM = "Depression Orb"
let TOOLTIP_EXTENDED = "The Hypnotist uses powerful magic to create an wisp of pure hatred and depression. The wisp is projected at opponents "+
                       "and it depresses all who get near it. Depressed opponents have slightly reduced speed and experience severe energy shortages. "+
                       "Has {0} seconds cooldown.".format(COOLDOWN.toToolTipTeal())

let SPELL_TMP_DEPRESSION_ORB = 'DOTP'

class DepressionOrb extends ChannelAbilityPreset
    construct(int newAbility, string hotkey, Pair<int , int> buttonPos)
        super(newAbility, 1, true)
        this.setName(TOOLTIP_NORM)
        this.setHotkeyNormal(hotkey)
        this.setCooldown(1, COOLDOWN)
        this.setManaCost(1, MANACOST)
        this.setIconNormal(Icons.bTNOrbOfCorruption)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.setArtDuration(1, 0)
        this.setTargetType(1, 2)
        this.setAnimationNames(ANIMATION_NAME)
        this.setHeroAbility(false)
        this.setTargetAttachmentPoint1("")
        this.setCasterAttachmentPoint1("")

@compiletime function createDepressionOrb()
    new DepressionOrb(SPELL_TMP_DEPRESSION_ORB, "R", new Pair(3, 0))

function startPeriodicAction(player owner, unit target, real timeout, int chance)
    doPeriodicallyTimed(0.1, timeout) cb ->
        if GetRandomInt(1, 100) <= chance
            new DummyCaster()
                ..owner(owner)
                ..origin(target.getPos()+vec2(100, 0))
                ..castTarget(DUMMY_ABILITY_ID, 1, Orders.thunderbolt, target)

function onCast()
    var target = GetSpellTargetUnit()
    var owner = GetSpellAbilityUnit().getOwner()

    startPeriodicAction(owner, target, 3, 18)
    doAfter(3, () -> startPeriodicAction(owner, target, 3, 15))
    doAfter(6, () -> startPeriodicAction(owner, target, 2, 8))

init
    registerSpellEffectEvent(ABILITY_ID, () -> onCast())
