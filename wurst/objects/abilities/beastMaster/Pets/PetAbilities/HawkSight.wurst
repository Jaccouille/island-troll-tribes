package HawkSight


// Standard Library Imports
import UpgradeObjEditing
import ChannelAbilityPreset
import ClosureEvents
import ClosureTimers
import Assets
import BuffObjEditing
import HashMap

// Local imports:
import LocalObjectIDs
import PlayerExtensions
import ToolTipsUtils
import ColorUtils
import StringExtensions
import LocalAssets
import Pets
import Transformation
import PetUpgradeHandler
import OraclePotion
import TimerUtils
import SpiritualGuidance

let BUFF_HAWK_SIGHT = compiletime(BUFF_ID_GEN.next())

let HAWK_SIGHT_TOOLTIP = "Hawk Sight"
let HAWK_SIGHT_TOOLTIP_EXT = "DADA"
let HAWK_SIGHT_DURATION = 5.
let HAWK_SIGHT_DURATION_INCREASE = 3.
let HAWK_SIGHT_COOLDOWN = 90.


@compiletime function createHawkSightDurationUpgrade() returns UpgradeDefinition
    return new UpgradeDefinition(UPGD_HAWK_SIGHT_DURATION)
        ..addEffectAbilityLevelBonus(1, 1, ABILITY_HAWK_SIGHT.toRawCode())
        ..setLevels(5)
        ..setName(1, "Hawk Sight Duration bonus")
        ..setIcon(1, Icons.bTNScout)

function createBuff()
    new BuffDefinition(BUFF_HAWK_SIGHT, 'Bstt')
        ..setIcon(Icons.bTNScout)
        ..setArtSpecial(1,"")
        ..setTooltipNormal(1, "Hawk Sight")
        ..setArtTarget(1, Abilities.magicSentryCaster)
        ..setTooltipNormalExtended(1, "This unit is sharing its vision with its master")

@compiletime function createUpgradeAbility() returns ChannelAbilityPreset
    return new ChannelAbilityPreset(ABILITY_PET_HAWK_SIGHT_UPGD, 5, true)
        ..setHeroAbility(false)
        ..presetCastRange(lvl -> 99999)
        ..presetCooldown(lvl -> 0)
        ..presetFollowThroughTime(lvl -> 0)
        ..presetArtDuration(lvl -> 0)
        ..setAnimationNames("")
        ..setName("Increase Hawk Sight")
        ..setIconNormal(Icons.bTNScout)
        ..presetButtonPosNormal(0, 2)
        ..setHotkeyNormal("Z")
        ..setArtCaster(Abilities.aIsmTarget)
        ..presetManaCost(lvl -> 2 * lvl)
        ..presetTooltipNormal(lvl -> makeToolTipNormUpgradeLevel("Z", HAWK_SIGHT_TOOLTIP, lvl))
        ..presetTooltipNormalExtended(lvl -> HAWK_SIGHT_TOOLTIP_EXT  + "\n\n"
                                     + toolTipUpgradeData("Current Damage Bonus", HAWK_SIGHT_DURATION_INCREASE.toInt(), lvl, COLOR_RED) + "\n"
                                     + toolTipUpgradeData("Max Damage Bonus", HAWK_SIGHT_DURATION_INCREASE.toInt(), 11, COLOR_RED)
                                     + " (+ {0} when playing as Druid)".format((HAWK_SIGHT_DURATION_INCREASE * PET_UPGRADE_BONUS).toString().color(COLOR_RED))
                                     )

@compiletime function createHawkSightAbility() returns ChannelAbilityPreset
    return new ChannelAbilityPreset(ABILITY_HAWK_SIGHT, 5, true)
        ..setName(HAWK_SIGHT_TOOLTIP)
        ..setIconNormal(Icons.bTNScout)
        ..setCooldown(1, HAWK_SIGHT_COOLDOWN)
        ..setManaCost(1, 0)
        ..presetHotkey("A")
        ..presetTooltipNormal(lvl -> makeToolTipNormLevel("A", HAWK_SIGHT_TOOLTIP, lvl))
        ..setTooltipNormalExtended(1, HAWK_SIGHT_TOOLTIP_EXT)
        ..presetCastRange(lvl -> 99999)
        ..presetFollowThroughTime(lvl -> 0)
        ..presetTargetTypes(Targettype.NONE)
        ..presetButtonPosNormal(0, 1)

@compiletime function createHawkSightRangeBonusAbility() returns AbilityDefinitionSightBonus
    return new AbilityDefinitionSightBonus(ABILITY_HAWK_SIGHT_INCREASE)
        ..setName("Hawk Sight Range Bonus")
        ..setIconNormal(Icons.bTNScout)
        ..setSightRangeBonus(1, 900)

HashMap<unit, timer> removalTimers = new HashMap<unit,timer>()

function onCastHawkSight(unit caster, int abilId)
    let level = caster.getAbilityLevel(abilId)

    if removalTimers.has(caster)
        removalTimers.getAndRemove(caster).release()

    caster.addAbility(SPELLBOOK_ABILITY_ID)
    caster.addAbility(ABILITY_HAWK_SIGHT_INCREASE)
    caster.getOwner().setAbilityAvailable(SPELLBOOK_ABILITY_ID, false)

    let t = getTimer()
    t.doAfter(HAWK_SIGHT_DURATION + level * HAWK_SIGHT_DURATION_INCREASE) ->
        caster.removeAbility(SPELLBOOK_ABILITY_ID)
        caster.removeAbility(BUFF_TRUE_SIGHT_AURA_ID)
        caster.removeAbility(ABILITY_HAWK_SIGHT_INCREASE)
        //caster.getOwner().setTechResearched(UPGD_HAWK_SIGHT, 0)
        removalTimers.remove(caster)
    removalTimers.put(caster, t)

init
    EventListener.onCast(ABILITY_HAWK_SIGHT, (unit caster) -> onCastHawkSight(caster, ABILITY_HAWK_SIGHT))
