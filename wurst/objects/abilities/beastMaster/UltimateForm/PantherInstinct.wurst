package PantherInstinct

// Standard lib Imports:
import ChannelAbilityPreset
import ClosureEvents
import ObjectIdGenerator
import ObjectIds
import Assets
import UpgradeObjEditing
import BuffObjEditing
import DamageEvent
import ClosureTimers
import InstantDummyCaster
import OrderIds

// Local Imports:
import PlayerExtensions
import LocalAssets
import ToolTipsUtils
import LocalObjectIDs
import TextTagUtils
import ColorUtils

let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let BUFF_ORIGINAL_ID = 'Bpsh' // PhaseShift

let DUMMY_ABIL_ID = compiletime(ABIL_ID_GEN.next())

let COOLDOWN = 20.
let DURATION = 4.
let TOOLTIP_NORM = "Panther Instinct"
let TOOLTIP_EXTENDED = "" +
    "Rendo uses the Panther inner instinct to predict incoming attack and dodge their damage." +
    makeToolTipDuration(DURATION, COOLDOWN)

@compiletime function createPantherInstinctBuff()
    new BuffDefinition(BUFF_ID, BUFF_ORIGINAL_ID)
        ..setIcon(Icons.bTNEvasion)
        ..setArtSpecial(1,"")
        ..setTooltipNormal(1, TOOLTIP_NORM)
        ..setTooltipNormalExtended(1, "This unit uses panther inner instinct to dodge incoming damage")

@compiletime function createDummyInnerFire()
    new AbilityDefinitionInnerFire(DUMMY_ABIL_ID)
        ..setDummyAbility()
        ..setDamageIncrease(1, 0)
        ..setDefenseIncrease(1, 0)
        ..setBuffs(1, BUFF_ID.toRawCode())
        ..setDurationHero(1, DURATION)
        ..setDurationNormal(1, DURATION)
        ..setName("Panther Instinct Dummy Cast")

@compiletime function createPantherInstinctAbility() returns ChannelAbilityPreset
    return new ChannelAbilityPreset(ABILITY_RENDO_PANTHER_INSTINCT, 1, true)
        ..presetManaCost(lvl -> 0)
        ..setLevelSkipRequirement(0)
        ..setRequiredLevel(1)
        ..setCheckDependencies(false)
        ..setCooldown(1, 1)
        ..setIconNormal(Icons.bTNEvasion)
        ..setName("Panther Instinct")
        ..setTooltipNormal(1, makeToolTipNorm("W", "Panther Instinct"))
        ..setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        ..setAnimationNames("stand - 3")
        ..setFollowThroughTime(1, 0)


init
    EventListener.onCast(ABILITY_RENDO_PANTHER_INSTINCT) (unit caster) ->
        InstantDummyCaster.castTarget(caster.getOwner(), DUMMY_ABIL_ID, 1, OrderIds.innerfire, caster)
        let dmgListener = DamageEvent.addListener()  ->
            let damaged = DamageEvent.getTarget()
            if damaged.hasAbility(BUFF_ID)
                let text = "Dodged " + DamageEvent.getAmount().round().toString()
                createFadingTextTag(caster, text, COLOR_RED)
                    ..setText(text, 8)
                    ..setVelocity(0, 0.06)
                    ..setLifespan(3)
                DamageEvent.setAmount(0)

        doPeriodically(0.3) cb ->
            if caster.isAlive() and caster.hasAbility(BUFF_ID)
                flashEffect(Abilities.mirrorImageCaster, caster.getPos())
            else
                destroy dmgListener
                destroy cb
