package MageMasher

// Standard lib Imports:
import DamageType
import LinkedList

// Third-party imports:
import ManaBurn

// Local Imports:
import LocalObjectIDs
import Classes

let DRAIN_MIN_AMOUNT = 10.
let DRAIN_PCT_AMOUNT = 0.05
let DRAIN_TARGET_CLASSES = asList(
    UNIT_MAGE,
    UNIT_PRIEST
)

function onDamage()
    // Look up the state.
    let origin = DamageEvent.getSource()
    let target = DamageEvent.getTarget()

    // Filter out irrelevant events.
    if not DamageEvent.getType() == DamageType.ATTACK
        return

    if not origin.hasItemById(ITEM_MAGE_MASHER)
        return

    // Prevent targeting allies.
    if origin.isAllyOf(target)
        return

    // Restrict the effects to spellcasters.
    if not DRAIN_TARGET_CLASSES.has(target.getTrollBaseClass())
        return

    // Apply the validated effect.
    origin.burnMana(
        target,
        max(DRAIN_PCT_AMOUNT * target.getMana(), DRAIN_MIN_AMOUNT)
    )

init
    DamageEvent.addListener() ->
        onDamage()
