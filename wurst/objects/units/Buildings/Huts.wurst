package Huts

// Standard library imports:
import Assets
import ObjectIds
import ClosureEvents
import AbilityObjEditing
import ObjectIdGenerator

// Local imports:
import LocalAssets
import LocalObjectIDs

public constant ABILITY_MUD_HUT_AURA = compiletime(ABIL_ID_GEN.next())


function createTentDefinition(int newId) returns BuildingDefinition
    return new BuildingDefinition(newId, UnitIds.tent2)
        ..setNormalAbilities(commaList(
            ABILITY_PACK_BUILDING,
            ABILITY_SHOP_SHARING,
            ABILITY_SLEEP_INSIDE_TENT
            )
        )
        ..setIconGameInterface(LocalIcons.bTNTent)
        ..setHitPointsMaximumBase(160)
        ..setSightRadiusNight(600)
        ..setPathingMap(PathingTextures.simpleSolid4x4)
        ..setPlacementRequires("unbuildable")
        ..setSightRadiusDay(900)
        ..setTooltipBasic("Set up Tent")
        ..setGroundTexture("")
        ..setArmorType(ArmorType.Small)
        ..setHitPointsMaximumBase(100)

@compiletime function createTent()
    createTentDefinition(UNIT_TENT)
    createTentDefinition(UNIT_TENT_FORTIFIED)
        ..setArmorType(ArmorType.Fortified)
        ..setHitPointsMaximumBase(160)
        ..setLumberCost(3)
        ..setName("Fortified Tent")


function createHut(int unitId) returns BuildingDefinition
    return new BuildingDefinition(unitId, UnitIds.spiritlodge)
        ..setNormalAbilities(commaList(
            LocalAbilityIds.inventoryBuilding,
            ABILITY_SHOP_SHARING,
            ABILITY_SLEEP_INSIDE_HUT
            )
        )
        ..setBuildTime(10)
        ..setHitPointsMaximumBase(250)
        ..setPathingMap(PathingTextures.simpleSolid4x4)
        ..setRequirements("")
        ..setResearchesAvailable("")
        ..setUnitsTrained("")
        ..setGroundTexture("")
        ..setUpgradesUsed("")
        ..setSelectionScale(3.0)

@compiletime function createTrollHut() returns BuildingDefinition
    return createHut(UNIT_TROLL_HUT)
        ..setScalingValue(0.8)
        ..setName("Troll Hut")
        ..setTooltipBasic("Build Troll hut")
        ..setTooltipExtended("A little clay, some sticks, and some hides and you have a very nice home.")
        ..setModelFile(LocalBuildings.trollHut)

@compiletime function createTrollMudHut() returns BuildingDefinition
    new AbilityDefinitionItemAuraDevotion(ABILITY_MUD_HUT_AURA)
        ..setIconNormal(Icons.pASBTNDevotion)
        ..setButtonPositionNormalX(2)
        ..setButtonPositionNormalY(2)
        ..setArmorBonus(1, 3.0)
        ..setItemAbility(false)
        ..setName("Igloo Resilience")
        ..setArtTarget("")
        ..setBuffs(1, BUFF_FORTITUDE.toRawCode())
        ..setTargetsAllowed(1, "air,friend,ground,hero,invulnerable,self,vulnerable")
        ..setTooltipNormal(1, "|cffffd700Home|r|n|nGives units around the Igloo armour")

    return createHut(UNIT_MUD_HUT)
        ..setIconGameInterface(LocalIcons.bTNIgloo)
        ..setTintingColorBlue(0)
        ..setModelFile(LocalBuildings.iglooFixed)
        ..setTintingColorGreen(255)
        ..setTintingColorRed(255)
        ..setTintingColorBlue(255)
        ..setName("Igloo")
        ..setTooltipBasic("Build Igloo")
        ..setTooltipExtended("An igloo made out of ice cube. Good for sleeping in")


init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_CONSTRUCT_FINISH) () ->
        let structure = GetConstructedStructure()
        if structure.getTypeId() == UNIT_MUD_HUT
            structure.addAbility(ABILITY_MUD_HUT_AURA)
            structure.makeAbilityPermanent(ABILITY_MUD_HUT_AURA, true)
